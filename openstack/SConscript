import os

Import('env')

localEnv = env.Clone()

target = 'libopenstack'
sources_c = [
    'openstack.c',
    #=== 02a-MAClow
    os.path.join('02a-MAClow','topology.c'),
    os.path.join('02a-MAClow','IEEE802154.c'),
    os.path.join('02a-MAClow','IEEE802154E.c'),
    os.path.join('02a-MAClow','adaptive_sync.c'),
    os.path.join('02a-MAClow','IEEE802154_security.c'),
    #=== 02b-MAChigh
    os.path.join('02b-MAChigh','neighbors.c'),
    os.path.join('02b-MAChigh','msf.c'),
    os.path.join('02b-MAChigh','schedule.c'),
    os.path.join('02b-MAChigh','sixtop.c'),
    #=== 03a-IPHC
    os.path.join('03a-IPHC','frag.c'),
    os.path.join('03a-IPHC','iphc.c'),
    os.path.join('03a-IPHC','openbridge.c'),
    #=== 03b-IPv6
    os.path.join('03b-IPv6','forwarding.c'),
    os.path.join('03b-IPv6','icmpv6.c'),
    os.path.join('03b-IPv6','icmpv6echo.c'),
    os.path.join('03b-IPv6','icmpv6rpl.c'),
    #=== 04-TRAN
    os.path.join('04-TRAN','udp.c'),
    os.path.join('04-TRAN','sock','sock.c'),
    #=== cross-layers
    os.path.join('cross-layers','idmanager.c'),
    os.path.join('cross-layers','openqueue.c'),
    os.path.join('cross-layers','openrandom.c'),
    os.path.join('cross-layers','packetfunctions.c'),
]
sources_h = [
    'openstack.h',
    #=== 02a-MAClow
    os.path.join('02a-MAClow','topology.h'),
    os.path.join('02a-MAClow','IEEE802154.h'),
    os.path.join('02a-MAClow','IEEE802154E.h'),
    os.path.join('02a-MAClow','adaptive_sync.h'),
    os.path.join('02a-MAClow','IEEE802154_security.h'),
    #=== 02b-MAChigh
    os.path.join('02b-MAChigh','neighbors.h'),
    os.path.join('02b-MAChigh','msf.h'),
    os.path.join('02b-MAChigh','schedule.h'),
    os.path.join('02b-MAChigh','sixtop.h'),
    #=== 03a-IPHC
    os.path.join('03a-IPHC','frag.h'),
    os.path.join('03a-IPHC','iphc.h'),
    os.path.join('03a-IPHC','openbridge.h'),
    #=== 03b-IPv6
    os.path.join('03b-IPv6','forwarding.h'),
    os.path.join('03b-IPv6','icmpv6.h'),
    os.path.join('03b-IPv6','icmpv6echo.h'),
    os.path.join('03b-IPv6','icmpv6rpl.h'),
    #=== 04-TRAN
    os.path.join('04-TRAN','udp.h'),
    os.path.join('04-TRAN','sock', 'sock_internal.h'),
    os.path.join('04-TRAN','sock','sock.h'),
    os.path.join('04-TRAN','sock','async.h'),
    os.path.join('04-TRAN','sock','sock_types.h'),
    os.path.join('04-TRAN','sock','async_types.h'),
    #=== cross-layers
    os.path.join('cross-layers','idmanager.h'),
    os.path.join('cross-layers','openqueue.h'),
    os.path.join('cross-layers','openrandom.h'),
    os.path.join('cross-layers','packetfunctions.h'),
]

if localEnv['board']=='python':

    for s in sources_c+sources_h:
        temp = localEnv.Objectify(
            target = localEnv.ObjectifiedFilename(s),
            source = s,
        )

    libopenstack = localEnv.Library(
        target = target,
        source = [localEnv.ObjectifiedFilename(s) for s in sources_c],
    )
    localEnv.Depends(libopenstack,[localEnv.ObjectifiedFilename(s) for s in sources_h])

else:

    localEnv.Append(
        CPPPATH =  [
            # inc
            os.path.join('#','inc'),
            # kernel
            os.path.join('#','kernel'),
            os.path.join('#','kernel', 'openos'),
            # drivers
            os.path.join('#','drivers','common'),
            os.path.join('#','drivers','common','crypto'),
            # openstack
            os.path.join('#','openstack'),
            os.path.join('#','openstack','02a-MAClow'),
            os.path.join('#','openstack','02b-MAChigh'),
            os.path.join('#','openstack','03a-IPHC'),
            os.path.join('#','openstack','03b-IPv6'),
            os.path.join('#','openstack','04-TRAN'),
            os.path.join('#','openstack','04-TRAN','sock'),
            os.path.join('#','openstack','cross-layers'),
            # openweb
            os.path.join('#','openweb'),
            # openapps
            os.path.join('#','openapps')
        ],
    )

    libopenstack = localEnv.Library(
        target = target,
        source = sources_c,
    )

Alias('libopenstack', libopenstack)
